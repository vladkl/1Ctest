
// Создает элементы справочника "Показатели производительности",
// соответствующие переданным счетчикам производительности ОС
//
// Параметры:
//  СчетчикиПроизводительности  - ТаблицаЗначений - Перечень серверов
//                  и счетчиков производительности, для которых необходимо создать 
//                  элементы справочника "Показатели производительности"
//
Процедура ОбновитьПоказателиПроизводительности(Серверы, СчетчикиОС) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Серверы.НомерСтроки КАК НомерСтроки,
	               |	Серверы.Сервер КАК Сервер,
	               |	Серверы.СпособСбораСчетчиков КАК СпособСбора,
	               |	Серверы.ЯзыкОС КАК ЯзыкОС,
	               |	Серверы.АгентЦКК КАК АгентЦКК
	               |ПОМЕСТИТЬ Серверы
	               |ИЗ
	               |	&Серверы КАК Серверы
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СчетчикиПроизводительности.НомерСтрокиСервера КАК НомерСтрокиСервера,
	               |	СчетчикиПроизводительности.Счетчик КАК Счетчик
	               |ПОМЕСТИТЬ Счетчики
	               |ИЗ
	               |	&СчетчикиПроизводительности КАК СчетчикиПроизводительности
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	Серверы.Сервер КАК Сервер,
	               |	Серверы.СпособСбора КАК СпособСбора,
	               |	Серверы.ЯзыкОС КАК ЯзыкОС,
	               |	Серверы.АгентЦКК КАК АгентЦКК,
	               |	Счетчики.Счетчик КАК Счетчик
	               |ПОМЕСТИТЬ СчетчикиРазвернутые
	               |ИЗ
	               |	Серверы КАК Серверы
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Счетчики КАК Счетчики
	               |		ПО Серверы.НомерСтроки = Счетчики.НомерСтрокиСервера
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ПоказателиПараметр.Ссылка КАК Ссылка,
	               |	МАКСИМУМ(ВЫБОР
	               |			КОГДА ПоказателиПараметр.Имя = ""Сервер""
	               |				ТОГДА ПоказателиПараметр.Значение
	               |			ИНАЧЕ NULL
	               |		КОНЕЦ) КАК Сервер,
	               |	МАКСИМУМ(ВЫБОР
	               |			КОГДА ПоказателиПараметр.Имя = ""Счетчик производительности""
	               |				ТОГДА ПоказателиПараметр.Значение
	               |			ИНАЧЕ NULL
	               |		КОНЕЦ) КАК Счетчик,
	               |	МАКСИМУМ(ВЫБОР
	               |			КОГДА ПоказателиПараметр.Имя = ""Язык ОС""
	               |				ТОГДА ПоказателиПараметр.Значение
	               |			ИНАЧЕ NULL
	               |		КОНЕЦ) КАК ЯзыкОС,
	               |	МАКСИМУМ(ВЫБОР
	               |			КОГДА ПоказателиПараметр.Имя = ""Агент ЦКК""
	               |				ТОГДА ПоказателиПараметр.Значение
	               |			ИНАЧЕ NULL
	               |		КОНЕЦ) КАК АгентЦКК,
	               |	МАКСИМУМ(ВЫБОР
	               |			КОГДА ПоказателиПараметр.Имя = ""Способ сбора счетчика""
	               |				ТОГДА ПоказателиПараметр.Значение
	               |			ИНАЧЕ NULL
	               |		КОНЕЦ) КАК СпособСбора
	               |ПОМЕСТИТЬ СуществующиеПоказатели
	               |ИЗ
	               |	Справочник.Показатели.Параметр КАК ПоказателиПараметр
	               |ГДЕ
	               |	ПоказателиПараметр.Ссылка.Родитель = ЗНАЧЕНИЕ(Справочник.Показатели.ПроизводительностьОборудования)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ПоказателиПараметр.Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СчетчикиРазвернутые.Сервер КАК Сервер,
	               |	СчетчикиРазвернутые.Счетчик КАК Счетчик,
	               |	СчетчикиРазвернутые.СпособСбора КАК СпособСбора,
	               |	СчетчикиРазвернутые.ЯзыкОС КАК ЯзыкОС,
	               |	СчетчикиРазвернутые.АгентЦКК КАК АгентЦКК,
	               |	СуществующиеПоказатели.Ссылка КАК Показатель
	               |ИЗ
	               |	СчетчикиРазвернутые КАК СчетчикиРазвернутые
	               |		ЛЕВОЕ СОЕДИНЕНИЕ СуществующиеПоказатели КАК СуществующиеПоказатели
	               |		ПО СчетчикиРазвернутые.Сервер = СуществующиеПоказатели.Сервер
	               |			И СчетчикиРазвернутые.Счетчик = СуществующиеПоказатели.Счетчик
	               |ГДЕ
	               |	(СуществующиеПоказатели.Ссылка ЕСТЬ NULL
	               |			ИЛИ СчетчикиРазвернутые.ЯзыкОС <> ЕСТЬNULL(СуществующиеПоказатели.ЯзыкОС, ЗНАЧЕНИЕ(Справочник.ЯзыкиОС.ПустаяСсылка))
	               |			ИЛИ СчетчикиРазвернутые.АгентЦКК <> ЕСТЬNULL(СуществующиеПоказатели.АгентЦКК, ЗНАЧЕНИЕ(Справочник.АгентыЦКК.ПустаяСсылка))
	               |			ИЛИ СчетчикиРазвернутые.СпособСбора <> ЕСТЬNULL(СуществующиеПоказатели.СпособСбора, ЗНАЧЕНИЕ(Перечисление.СпособыСбораСчетчиков.ПустаяСсылка)))";
	
	Запрос.УстановитьПараметр("Серверы", Серверы);
	Запрос.УстановитьПараметр("СчетчикиПроизводительности", СчетчикиОС);
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		Если ЗначениеЗаполнено(Выборка.Показатель) Тогда
			ПоказательОбъект = Выборка.Показатель.ПолучитьОбъект();
		Иначе
			ПоказательОбъект = Справочники.Показатели.СоздатьЭлемент();
			ПоказательОбъект.Наименование = Выборка.Сервер + Строка(Выборка.Счетчик);
			ПоказательОбъект.УстановитьНовыйКод("_34_P0");
			ПоказательОбъект.Родитель = Справочники.Показатели.ПроизводительностьОборудования;
			ПоказательОбъект.ГраницаЗЖ = 0;
			ПоказательОбъект.ГраницаЖК = 0;
			ПоказательОбъект.ЕдиницаИзмерения = Перечисления.ЕдиницыИзмерения.ПустаяСсылка();
			ПоказательОбъект.Интерактивный = Истина;
			ТипЛинии = ТипЛинииДиаграммы.Сплошная;
			ТолщинаЛинии = 1;
			ПоказательОбъект.Линия = Новый ХранилищеЗначения(Новый Линия(ТипЛинии, ТолщинаЛинии));
			ПоказательОбъект.Масштаб = 1;
			ПоказательОбъект.Тип = Перечисления.ТипыПоказателей.ПроизвольныеСчетчикиОС;
			ПоказательОбъект.Цвет = Новый ХранилищеЗначения(WebЦвета.Черный);
			
			ПоддержкаСУБД = ПоказательОбъект.ПоддержкаСУБД.Добавить();
			ПоддержкаСУБД.АвтоматическийРежим = Истина;
			ПоддержкаСУБД.СмешанныйРежим = Истина;
			ПоддержкаСУБД.УправляемыйРежим = Истина;
		КонецЕсли;
		
		ПоказательОбъект.Параметр.Очистить();
		ПараметрПоказателя = ПоказательОбъект.Параметр.Добавить();
		ПараметрПоказателя.Имя = "Сервер";
		ПараметрПоказателя.Значение = Выборка.Сервер;
		
		ПараметрПоказателя = ПоказательОбъект.Параметр.Добавить();
		ПараметрПоказателя.Имя = "Счетчик производительности";
		ПараметрПоказателя.Значение = Выборка.Счетчик;
		
		ПараметрПоказателя = ПоказательОбъект.Параметр.Добавить();
		ПараметрПоказателя.Имя = "Способ сбора счетчика";
		ПараметрПоказателя.Значение = Выборка.СпособСбора;
		
		ПараметрПоказателя = ПоказательОбъект.Параметр.Добавить();
		ПараметрПоказателя.Имя = "Язык ОС";
		ПараметрПоказателя.Значение = Выборка.ЯзыкОС;
		
		ПараметрПоказателя = ПоказательОбъект.Параметр.Добавить();
		ПараметрПоказателя.Имя = "Агент ЦКК";
		ПараметрПоказателя.Значение = Выборка.АгентЦКК;
		
		ПоказательОбъект.Записать();
	КонецЦикла;
	
КонецПроцедуры // ОбновитьПоказателиПроизводительности()

