
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИБ = Параметры.ИБ;
	ХранилищеПоказателей = Новый ХранилищеЗначения(Параметры.Показатели);
	НачалоИнтервала = Параметры.НачалоИнтервала;
	КонецИнтервала = Параметры.КонецИнтервала;
	
КонецПроцедуры

&НаКлиенте
// Выбор имени файла
//
Процедура ИмяФайлаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ДиалогВыбора = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	ДиалогВыбора.Фильтр = "ZIP архив(*.zip)|*.zip";
	ДиалогВыбора.Расширение = "zip";
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ИмяФайлаНачалоВыбораЗавершение", ЭтотОбъект);
	ДиалогВыбора.Показать(ОписаниеОповещения);
	
КонецПроцедуры

// <Описание процедуры>
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
&НаКлиенте
Процедура ИмяФайлаНачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		ИмяФайла = Результат[0];
	КонецЕсли;
	
КонецПроцедуры // ОписаниеОповещения()


// Экспортирует данные замеров информационной базы на указанном интервале
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
// Возвращаемое значение:
//   <Тип.Вид>   - <описание возвращаемого значения>
//
&НаСервереБезКонтекста
Функция ЭкспортироватьНаСервере(ИБ, ХранилищеПоказателей, НачалоИнтервала, КонецИнтервала)
	
	ИмяВременногоФайлаZIP = ПолучитьИмяВременногоФайла("zip");
	ВременныйКаталог = ОбщегоНазначенияКлиентСервер.ИмяКаталога(ИмяВременногоФайлаZIP) + "/" + Строка(Новый УникальныйИдентификатор);
	Начало = НачалоИнтервала;
	Конец = КонецИнтервала;
	//ТекущийНомер = 0;
	Всего = 0;
	ШиринаИнтервалаПакета = 3600;
	КоличествоПакетов = ОбщегоНазначенияКлиентСервер.ОкрВверх((Конец - Начало) / ШиринаИнтервалаПакета);
	//ЭлементыФормы.ИндикаторПакетов.МаксимальноеЗначение = КоличествоПакетов;
	ИндикаторПакетов = 0;
	Показатели = ХранилищеПоказателей.Получить();
	
	СоздатьКаталог(ВременныйКаталог);
	АрхивZIP = Новый ЗаписьZipФайла(ИмяВременногоФайлаZIP);
	
	Пока НачалоИнтервала <= Конец Цикл
		КонецИнтервала = НачалоИнтервала + ШиринаИнтервалаПакета;
		
		Если КонецИнтервала > Конец Тогда
			КонецИнтервала = Конец;
		КонецЕсли;
		
		ИмяВременногоФайла = ВременныйКаталог + "/" + ОбщегоНазначенияКлиентСервер.ИмяФайлаПоВремени(НачалоИнтервала) + СловарьКлиентСервер.Получить("РасширениеФайлаПакетаДанных");
		ЭкспортироватьИнтервал(ИмяВременногоФайла, ИБ, НачалоИнтервала, КонецИнтервала);
		АрхивZIP.Добавить(ИмяВременногоФайла);
		
		НачалоИнтервала = КонецИнтервала + 1;
		ИндикаторПакетов = ИндикаторПакетов + 1;
	КонецЦикла;
	
	ИмяФайлаОписания = ВременныйКаталог + "/Description.txt";
	ЗаполнитьОписание(ИмяФайлаОписания, ИБ, Показатели, НачалоИнтервала, КонецИнтервала);
	АрхивZIP.Добавить(ИмяФайлаОписания);
	
	АрхивZIP.Записать();
	ДанныеЭкспорта = Новый ДвоичныеДанные(ИмяВременногоФайлаZIP);
	АдресВоВременномХранилище = ПоместитьВоВременноеХранилище(ДанныеЭкспорта);
	
	УдалитьФайлы(ВременныйКаталог);
	УдалитьФайлы(ИмяВременногоФайлаZIP);
	//ТекущийНомер = 0;
	ИндикаторПакетов = 0;
	Описание = "";
	ОтладкаКлиентСервер.Результат("Экспорт", Всего);
	Всего = 0;
	
	Возврат АдресВоВременномХранилище;
	
КонецФункции // ЭкспортироватьНаСервере()

&НаКлиенте
Процедура КомандаЭкспорт(Команда)
	
	Если ПустаяСтрока(ИмяФайла) Тогда
		ИнтерфейсыКлиент.Предупредить(СловарьКлиентСервер.Получить("ПредупреждениеНеУказаноИмяФайла"));
		Возврат;
	КонецЕсли;
	
	Если глКонтекст = Неопределено Или Не Контекст.ПросмотрВключен() Тогда
		ИнтерфейсыКлиент.Предупредить(СловарьКлиентСервер.Получить("ПредупреждениеНеобходимРежимПросмотра"));
		Возврат;
	КонецЕсли;
	
	Попытка
		ОтладкаКлиентСервер.Действие("Экспорт", ИмяФайла);
		АдресВоВременномХранилище = ЭкспортироватьНаСервере(ИБ, ХранилищеПоказателей, НачалоИнтервала, КонецИнтервала);
		ФайлЭкспорта = ПолучитьИзВременногоХранилища(АдресВоВременномХранилище);
		ФайлЭкспорта.Записать(ИмяФайла);
	Исключение
		ОтладкаКлиентСервер.Ошибка(ИнформацияОбОшибке());
		Возврат;
	КонецПопытки;
	
	ИнтерфейсыКлиент.Предупредить(СловарьКлиентСервер.Получить("ПредупреждениеДанныеЭкспортированы"));
	
КонецПроцедуры

&НаСервереБезКонтекста
// Экспортировать данные замеров информационной базы на указанном интервале
//
// Параметры:
//  ИмяФайла - Строка, имя файла для экспорта
//  ИБ - СправочникСсылка.ИнформационноаяБаза
//  Начало - ДатаВремя, начало интервала поиска
//  Конец - ДатаВремя, конец интервала поиска
//
Процедура ЭкспортироватьИнтервал(ИмяФайла, ИБ, Начало, Конец)
	
	Описание = СловарьКлиентСервер.Получить("ПоискЭкспортируемыхДанных");
	ЭкспортируемыеОбъекты = Обмен.НайтиЭкспортируемыеОбъекты(ИБ, Начало, Конец);
	НаборЖурналаПоказателей = Обмен.ПолучитьНаборЗаписейРегистра("ЖурналПоказателей", "Период, Год, Месяц, День, Час, Минута, Показатель, НомерЗаписи, Значение", ИБ, Начало, Конец);
	НаборЗакладок = Обмен.ПолучитьНаборЗаписейРегистра("Закладки", "Период, Наименование, Описание", ИБ, Начало, Конец);
	
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.ОткрытьФайл(ИмяФайла);
	
	ЗаписьXML.ЗаписатьОбъявлениеXML();
	ЗаписьXML.ЗаписатьНачалоЭлемента(СловарьКлиентСервер.Получить("XMLДанные"));
	КоличествоЭкспортируемыхОбъектов = ЭкспортируемыеОбъекты.Количество() + 2;
	ЗаписьXML.ЗаписатьАтрибут(СловарьКлиентСервер.Получить("XMLКоличество"), Формат(КоличествоЭкспортируемыхОбъектов, "ЧГ=0"));
	Индикатор = 0;
	
	Для Каждого ЭкспортируемыйОбъект Из ЭкспортируемыеОбъекты Цикл
		Объект = ЭкспортируемыйОбъект.Ключ.ПолучитьОбъект();
		Описание = Объект;
		ЗаписатьXML(ЗаписьXML, Объект);
		Индикатор = Индикатор + 1;
	КонецЦикла;
	
	ЗаписатьXML(ЗаписьXML, НаборЖурналаПоказателей);
	Индикатор = Индикатор + 1;
	ЗаписатьXML(ЗаписьXML, НаборЗакладок);
	Индикатор = Индикатор + 1;
	
	ЗаписьXML.ЗаписатьКонецЭлемента();
	ЗаписьXML.Закрыть();
	Индикатор = 0;
	
КонецПроцедуры // ЭкспортироватьИнтервал()

&НаСервереБезКонтекста
// Заполнить файл описания экспорта
//
// Параметры:
//  ИмяФайлаОписания - Строка, имя файла для размещения описания
//
Процедура ЗаполнитьОписание(ИмяФайлаОписания, ИБ, Показатели, НачалоИнтервала, КонецИнтервала)
	
	Текст = Новый ЗаписьТекста(ИмяФайлаОписания);
	Текст.ЗаписатьСтроку("Версия              : " + Метаданные.Версия);
	Текст.ЗаписатьСтроку("Дата экспорта       : " + ТекущаяДата());
	Текст.ЗаписатьСтроку("Информационная база : " + ИБ);
	Текст.ЗаписатьСтроку("Интервал            : с " + НачалоИнтервала + " по " + КонецИнтервала);
	Текст.ЗаписатьСтроку("Показатели          : ");
	
	Для Каждого СтрокаПоказателя Из Показатели Цикл
		Текст.ЗаписатьСтроку("        - " + СтрокаПоказателя.Показатель);
	КонецЦикла;
	
	Текст.Закрыть();
	
КонецПроцедуры // ЗаполнитьОписание()

