///////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ БЛОКИРОВКИ ДАННЫХ

// Заблокировать объект указанным значением и режимом
//
// Параметры:
//  Ссылка - Блокируемая ссылка
//  Режим - РежимБлокировкиДанных, по умолчанию - Исключительный
//  Пространство - Строка, имя пространства блокировки,
//                 по-умолчанию определяется автоматически
//
Процедура ЗаблокироватьСсылку(Ссылка, Режим = Неопределено, Пространство = Неопределено) Экспорт
	
	Блокировка = Новый БлокировкаДанных;
	СтрокаБлокировки = Блокировка.Добавить(?(Пространство = Неопределено, Ссылка.Метаданные().ПолноеИмя(), Пространство));
	СтрокаБлокировки.Режим = ?(Режим = Неопределено, РежимБлокировкиДанных.Исключительный, Режим);
	СтрокаБлокировки.УстановитьЗначение("Ссылка", Ссылка);
	Блокировка.Заблокировать();
	
КонецПроцедуры // ЗаблокироватьСсылку()

// Подготовить объект к изменению.
// После вызова этой процедуры обязательно вызвать или
// ЗавершитьИзменениеОбъекта() или ОтменитьИзменениеОбъекта
//
// Параметры:
//  Ссылка - на изменяемый объект
//  Пространство - Строка, имя пространства блокировки
//                 (по-умолчанию определяется автоматически)
//
// Возвращаемое значение:
//  Объект - на который указывала ссылка
//
Функция НачатьИзменениеОбъекта(Ссылка, Пространство = Неопределено) Экспорт
	
	НачатьТранзакцию();
	ЗаблокироватьСсылку(Ссылка,, Пространство);
	Объект = Ссылка.ПолучитьОбъект();
	Возврат Объект;
	
КонецФункции // НачатьИзменениеОбъекта()

// Завершить изменение объекта, начатое вызовом функции
// НачатьИзменениеОбъекта()
//
// Параметры:
//  Объект - для которого завершается изменение
//
Процедура ЗавершитьИзменениеОбъекта(Объект, ЗаписатьИзменения = Истина) Экспорт
	
	Если ЗаписатьИзменения Тогда
		Объект.Записать();
	КонецЕсли;
	
	ЗафиксироватьТранзакцию();
	
КонецПроцедуры // ЗавершитьИзменениеОбъекта()

// Отменить изменение объекта, начатое вызовом функции
// НачатьИзменениеОбъекта()
//
// Параметры:
//  Объект - для которого отменяется изменение
//
Процедура ОтменитьИзменениеОбъекта(Объект) Экспорт
	
	ОтменитьТранзакцию();
	
КонецПроцедуры // ЗавершитьИзменениеОбъекта()
