///////////////////////////////////////////////////////////////////////////////
// СЧЕТЧИКИ ПРОИЗВОДИТЕЛЬНОСТИ

// Сформировать имя счетчика производительности
//
// Параметры:
//  Компьютер - Строка, имя или IP-адрес компьютера с которого будут получаться
//              значения счетчиков производительности. Можно не указывать, если
//              значения получаются со своего компьютера
//  Объект -  Строка, имя физического компонента системы (процессор, диск, ...)
//            или объекта системы (процессы, потоки, ...)
//  Экземпляр - Строка, экземпляр объекта
//  Родитель - Строка, родительский экземпляр объекта
//  Индекс - Строка, номер экземпляра объекта, применимый если несколько
//           экземпляров одного объекта имеют одинаковое имя
//
// Возвращаемое значение:
//  Строка - полное имя счетчика производительности
//
Функция ИмяСчетчика(Объект,
                    Счетчик,
                    Компьютер = "",
                    Экземпляр = "",
                    Родитель = "",
                    Индекс = "") Экспорт
	
	ПолноеИмя = "";
	
	Если Не ПустаяСтрока(Компьютер) Тогда
		ПолноеИмя = "\\" + Компьютер;
	КонецЕсли;
	
	ПолноеИмя = ПолноеИмя + "\" + Объект;
	ЕстьРодитель = Не ПустаяСтрока(Родитель);
	ЕстьЭкземпляр = Не ПустаяСтрока(Экземпляр);
	ЕстьИндекс = Не ПустаяСтрока(Индекс);
	
	Если ЕстьРодитель Или ЕстьЭкземпляр Или ЕстьИндекс Тогда
		ПолноеИмя = ПолноеИмя + "(";
		
		Если ЕстьРодитель Тогда
			ПолноеИмя = ПолноеИмя + Родитель + "/";
		КонецЕсли;
		
		Если ЕстьЭкземпляр Тогда
			ПолноеИмя = ПолноеИмя + Экземпляр;
		КонецЕсли;
		
		Если ЕстьИндекс Тогда
			ПолноеИмя = ПолноеИмя + "#" + Индекс;
		КонецЕсли;
		
		ПолноеИмя = ПолноеИмя + ")";
	КонецЕсли;
	
	ПолноеИмя = ПолноеИмя + "\" + Счетчик;
	Возврат ПолноеИмя;
	
КонецФункции // ИмяСчетчика()

// Получить имя объекта счетчиков SQL Server
//
// Параметры:
//  ИмяСервера - Строка, полное имя сервера СУБД
//  Класс - Строка, класса объекта
//
// Возвращаемое значение:
//  Строка - имя объекта
//
Функция ИмяОбъектаСчетчиковMSSQL(ИмяСервера, Класс) Экспорт
	
	СоставИмени = ОбщегоНазначенияКлиентСервер.РазделитьСтроку(ИмяСервера, "\");
	Имя = ?(СоставИмени.Количество() > 1, "MSSQL$" + СоставИмени[1], "SQLServer");
	Возврат Имя + ":" + Класс;
	
КонецФункции // ИмяОбъектаСчетчиковMSSQL()

// Получить имя сервера из имени сервера СУБД
//
// Параметры:
//  ИмяСервера - Строка, полное имя сервера СУБД
//
// Возвращаемое значение:
//  Строка - имя компьютера, на котором запущен сервер СУБД
//
Функция ИмяСервераMSSQL(ИмяСервера) Экспорт
	
	СоставИмени = ОбщегоНазначенияКлиентСервер.РазделитьСтроку(ИмяСервера, "\");
	Возврат СоставИмени[0];
	
КонецФункции // ИмяОбъектаСчетчиковMSSQL()

// Получить текст запроса, возвращающего значение счетчика количества ожиданий на блокировках MSSQL
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Строка - Текст запроса
//
Функция ЗапросКоличествоТаймаутовMSSQL() Экспорт
	
	ТекстЗапроса = 
	"select cntr_value from sys.dm_os_performance_counters
	|where 
	|object_name = 'SQLServer:Locks'
	|and instance_name = '_Total'
	|and counter_name = 'Lock Timeouts (timeout > 0)/sec'";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Получить текст запроса, возвращающего значение счетчика количества взаимоблокировок MSSQL
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Строка - Текст запроса
//
Функция ЗапросКоличествоВзаимоблокировокMSSQL() Экспорт
	
	ТекстЗапроса = 
	"select cntr_value from sys.dm_os_performance_counters
	|where 
	|object_name = 'SQLServer:Locks'
	|and instance_name = '_Total'
	|and counter_name = 'Number of Deadlocks/sec'";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Включить сбор счетчика производительности
//
Функция ВключитьСборСчетчикаПроизводительности(Счетчики, ИмяСервераСУБД, ВозможныеИменаСчетчиков, ПредставлениеСчетчика) Экспорт
	
	ИндексПоказателя = Неопределено;
	
	СчетчикДобавлен = Ложь;
	Для Каждого ИмяСчетчика Из ВозможныеИменаСчетчиков Цикл
		Попытка
			ИндексПоказателя = КипВнешнийКомпонент.ДобавитьСчетчик(
													Счетчики,
													ИмяСчетчика);
			СчетчикДобавлен = Истина;
			Прервать;
		Исключение
		КонецПопытки;
	КонецЦикла;
	Если Не СчетчикДобавлен Тогда
		СчетчикиСтрокой = ОбщегоНазначенияКлиентСервер.ОбъединитьСтроку(ВозможныеИменаСчетчиков, "; ");
		ВызватьИсключение "Ошибка получения счетчика performance monitor """ + ПредставлениеСчетчика + """." + Символы.ПС
						+ "На сервере " + ИмяСервераСУБД + " не удалось включить сбор показателей ни для одного из возможных вариантов счетчика:" + Символы.ПС
						+ СчетчикиСтрокой;
	КонецЕсли;
	
	КипВнешнийКомпонент.СобратьЗначенияСчетчиков(Счетчики);
	КипВнешнийКомпонент.СобратьЗначенияСчетчиков(Счетчики);
	
	Возврат ИндексПоказателя;
	
КонецФункции
