
#Если Не ВебКлиент Тогда

// Загрузить словарь строк
//
// Параметры:
//  ДокументСловаря - Строка, XML документ содержащий строки словаря по схеме
//                 "http://v8.1c.ru/v8/pmc-dictionary-instance"
//
Функция Загрузить(ДокументСловаря) Экспорт
	
	Схема = "http://v8.1c.ru/v8/pmc-dictionary-instance";
	ТипОбъектаСтроки = ФабрикаXDTO.Тип(Схема, "String");
	
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.УстановитьСтроку(ДокументСловаря);
	ЧтениеXML.ПерейтиКСодержимому(); // Прейти к <dictionary...>
	ЧтениеXML.Прочитать();           // Пропустить <dictionary...>
	
	ДанныеСловаря = Новый Соответствие;
	
	Пока Истина Цикл
		
		Если ЧтениеXML.ЛокальноеИмя = "dictionary" Тогда
			Прервать;
		КонецЕсли;
		
		Событие = ФабрикаXDTO.ПрочитатьXML(ЧтениеXML, ТипОбъектаСтроки);
			
		Если Событие = Неопределено Тогда
			Прервать;
		КонецЕсли;
		
		Значения = "ru=""" + Событие.ru + """";
		ДанныеСловаря.Вставить(ВРег(Событие.name), Значения);
		
	КонецЦикла;
	
	Возврат ДанныеСловаря;
	
КонецФункции // Загрузить()

// Получить строку по имени с возможностью указания до трех параметров,
// если строка содержит шаблоны подстановки вида {n}, где n=[0..2]
//
// Параметры:
//  ИмяСтроки - Строка, имя получаемой строки
//  Параметр0 - Произвольный, значение для шаблона {0}
//  Параметр1 - Произвольный, значение для шаблона {1}
//  Параметр2 - Произвольный, значение для шаблона {2}
//
// Возвращаемое значение:
//  Строка, искомая строка, если строка не найдена, генерируется исключение
//
Функция Получить(ИмяСтроки,
                 Параметр0 = Неопределено,
                 Параметр1 = Неопределено,
                 Параметр2 = Неопределено) Экспорт
	
	#Если Клиент Тогда
		ТаблицаСловаря = Словарь();
	#Иначе
		ТаблицаСловаря = ПараметрыСеанса.ТаблицаСловаря.Получить();
	#КонецЕсли
	
	Строка = ТаблицаСловаря[ВРег(ИмяСтроки)];
	
	Если ПустаяСтрока(Строка) Тогда
		ВызватьИсключение "Не найдена строка словаря (" + ИмяСтроки + ")";
	КонецЕсли;
	
	Строка = НСтр(Строка);
	
	Если Параметр0 <> Неопределено Тогда
		Строка = СтрЗаменить(Строка, "{0}", Параметр0);
		
		Если Параметр1 <> Неопределено Тогда
			Строка = СтрЗаменить(Строка, "{1}", Параметр1);
			
			Если Параметр2 <> Неопределено Тогда
				Строка = СтрЗаменить(Строка, "{2}", Параметр2);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Строка;
	
КонецФункции // Получить()

#КонецЕсли