
#Область ПрограммныйИнтерфейс

Процедура ЗаписатьДанные(Владелец, Данные) Экспорт
	
	ХранилищеЗначения = Новый ХранилищеЗначения(Данные, Новый СжатиеДанных(9));
	МенеджерЗаписи = РегистрыСведений.БезопасноеХранилище.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ВладелецХранилища = Владелец;
	МенеджерЗаписи.ДанныеХранилища = ХранилищеЗначения;
	МенеджерЗаписи.Записать(Истина);
	
КонецПроцедуры

Функция ПолучитьДанные(Владелец) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ДанныеХранилища
	|ИЗ
	|	РегистрСведений.БезопасноеХранилище
	|ГДЕ
	|	ВладелецХранилища = &ВладелецХранилища
	|";
	
	Запрос.УстановитьПараметр("ВладелецХранилища", Владелец);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Неопределено;
	Иначе
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		Возврат Выборка.ДанныеХранилища.Получить();
	КонецЕсли;
	
КонецФункции

Процедура УдалитьДанные(Владелец) Экспорт
	
	НаборЗаписей = РегистрыСведений.БезопасноеХранилище.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ВладелецХранилища.Установить(Владелец);
	НаборЗаписей.Записать(Истина);
	
КонецПроцедуры

#КонецОбласти
