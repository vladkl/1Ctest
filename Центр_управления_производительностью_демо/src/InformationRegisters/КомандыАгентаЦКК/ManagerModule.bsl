#Область ПрограммныйИнтерфейс

// Добавляет команду для агента ЦКК
//
// Параметры:
//  АгентЦКК   - СправочникСсылка.АгентыЦКК       - агент ЦКК для которого добавляется команда.
//  ТипКоманды - Перечисление.ТипыКомандАгентаЦКК - тип команды.
//  Команда    - Соответствие - параметры команды.
//
Процедура ДобавитьКоманду(АгентЦКК, ТипКоманды, Команда) Экспорт
    
    НаборЗаписей = РегистрыСведений.КомандыАгентаЦКК.СоздатьНаборЗаписей();
    НаборЗаписей.Отбор.АгентЦКК.Установить(АгентЦКК);
    НаборЗаписей.Отбор.ТипКоманды.Установить(ТипКоманды);
    НаборЗаписей.Отбор.СтатусКоманды.Установить(Перечисления.СтатусыКомандАгентаЦКК.ГотоваКОтправке);
    НаборЗаписей.Прочитать();
    
    НаборЗаписейНовый = РегистрыСведений.КомандыАгентаЦКК.СоздатьНаборЗаписей();
    Для Каждого ТекЗапись Из НаборЗаписей Цикл
        НовЗапись = НаборЗаписейНовый.Добавить();
        НовЗапись.Период = ТекЗапись.Период;
        НовЗапись.АгентЦКК = ТекЗапись.АгентЦКК;
        НовЗапись.ТипКоманды = ТекЗапись.ТипКоманды;
        НовЗапись.СтатусКоманды = Перечисления.СтатусыКомандАгентаЦКК.Отменена;
        НовЗапись.УникальныйИдентификаторЗаписи = ТекЗапись.УникальныйИдентификаторЗаписи;
        НовЗапись.Команда = ТекЗапись.Команда;
    КонецЦикла;
    
    НаборЗаписей.Очистить();
    
    НачатьТранзакцию();
    
    Попытка
        
        НаборЗаписей.Записать(Истина);
        НаборЗаписейНовый.Записать(Ложь);
        
        ЗафиксироватьТранзакцию();
        
    Исключение
        
        ОтменитьТранзакцию();
        ВызватьИсключение ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
        
    КонецПопытки;
    
    УникальныйИдентификаторЗаписи = Новый УникальныйИдентификатор; 
    Команда.Вставить("GuidCommand", Строка(УникальныйИдентификаторЗаписи));
    
    ЗаписьJSON = Новый ЗаписьJSON();
	ЗаписьJSON.УстановитьСтроку(Новый ПараметрыЗаписиJSON(ПереносСтрокJSON.Нет));
	ЗаписатьJSON(ЗаписьJSON, Команда);
	КомандаJSON = ЗаписьJSON.Закрыть();
    
    МенеджерЗаписи = РегистрыСведений.КомандыАгентаЦКК.СоздатьМенеджерЗаписи();
    МенеджерЗаписи.Период = ТекущаяУниверсальнаяДата();
    МенеджерЗаписи.АгентЦКК = АгентЦКК;
    МенеджерЗаписи.СтатусКоманды = Перечисления.СтатусыКомандАгентаЦКК.ГотоваКОтправке;
    МенеджерЗаписи.УникальныйИдентификаторЗаписи = УникальныйИдентификаторЗаписи;
    МенеджерЗаписи.ТипКоманды = ТипКоманды;
    МенеджерЗаписи.Команда = КомандаJSON;
    
    МенеджерЗаписи.Записать(Ложь);    
    
КонецПроцедуры

// Установливает статус команды
//
// Параметры:
//  УникальныйИдентификаторЗаписи - УникальныйИдентификатор - уникальный идентификатор записи.
//  СтатусКоманды                 - ПеречислениеСсылка.СтатусыКомандАгентаЦКК - новый статус команды.
//
Процедура УстановитьСтатусКоманды(УникальныйИдентификаторЗаписи, СтатусКоманды) Экспорт
    
    НаборЗаписей = РегистрыСведений.КомандыАгентаЦКК.СоздатьНаборЗаписей();
    НаборЗаписей.Отбор.УникальныйИдентификаторЗаписи.Установить(УникальныйИдентификаторЗаписи);
    НаборЗаписей.Прочитать();
    
    Если НаборЗаписей.Количество() = 0 Тогда
        
        ВызватьИсключение "Не найдена запись с идентификатором " + УникальныйИдентификаторЗаписи;
        
    ИначеЕсли НаборЗаписей.Количество() = 1 Тогда
        
        ТекЗапись = НаборЗаписей[0];
        ТекЗапись.СтатусКоманды = СтатусКоманды;
        
        НаборЗаписей.Записать(Истина);
        
    Иначе
        
        ВызватьИсключение "Найден несколько записей с идентификатором " + УникальныйИдентификаторЗаписи;
        
    КонецЕсли;
                
КонецПроцедуры

// Функция - Получить команду
//
// Параметры:
//  АгентЦКК   - СправочникСсылка.АгентыЦКК       - агент ЦКК получения команды.
//  ТипКоманды - Перечисление.ТипыКомандАгентаЦКК - тип команды.
// 
// Возвращаемое значение:
//   - Строка
//
Функция ПолучитьКомандуОтправки(АгентЦКК, ТипКоманды, НовыйСтатус = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|   УникальныйИдентификаторЗаписи,
	|   Команда
	|ИЗ
	|   РегистрСведений.КомандыАгентаЦКК
	|ГДЕ
	|   АгентЦКК = &АгентЦКК
	|   И ТипКоманды = &ТипКоманды
	|   И СтатусКоманды = &СтатусКоманды
	|УПОРЯДОЧИТЬ ПО
	|   Период
	|";
	
	Запрос.УстановитьПараметр("АгентЦКК", АгентЦКК);
	Запрос.УстановитьПараметр("ТипКоманды", ТипКоманды);
	Запрос.УстановитьПараметр("СтатусКоманды", Перечисления.СтатусыКомандАгентаЦКК.ГотоваКОтправке);
	
	Результат = Запрос.Выполнить();
	
	Если НЕ Результат.Пустой() Тогда
		
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		
		КомандаАгента = Выборка.Команда;
		
		Если НовыйСтатус <> Неопределено Тогда
			УстановитьСтатусКоманды(Выборка.УникальныйИдентификаторЗаписи, НовыйСтатус);
		КонецЕсли;
		
	Иначе
		
		КомандаАгента = Неопределено;
		
	КонецЕсли;
	
	Возврат КомандаАгента;
	
КонецФункции

#КонецОбласти