
// Возвращает признак необходимости сбора счетчиков производительности
// агентом ЦКК
//
// Параметры:
//  ИнформационнаяБаза  - СправочникСсылка.ИнформационныеБазы - Информационная база
//
//  АгентЦКК  - СправочникСсылка.АгентыЦКК - Агент ЦКК
//
// Возвращаемое значение:
//  Булево - признак необходимости сбора счетчиков
// 
Функция ВключенСборСчетчиков(АгентЦКК) Экспорт
	
	Результат = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	МАКСИМУМ(СостояниеИБ.ВключенСборСчетчиков) КАК ВключенСборСчетчиков,
	               |	МАКСИМУМ(СостояниеИБ.ТестСбораСчетчиков) КАК ТестСбораСчетчиков
	               |ИЗ
	               |	(ВЫБРАТЬ
	               |		ЕСТЬNULL(СостояниеСбораПоказателей.ВключенСборСчетчиков, ЛОЖЬ) КАК ВключенСборСчетчиков,
	               |		ЛОЖЬ КАК ТестСбораСчетчиков
	               |	ИЗ
	               |		Справочник.ИнформационныеБазы.Серверы КАК ИнформационныеБазыСерверы
	               |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СостояниеСбораПоказателей КАК СостояниеСбораПоказателей
	               |			ПО ИнформационныеБазыСерверы.Ссылка = СостояниеСбораПоказателей.ИнформационнаяБаза
	               |	ГДЕ
	               |		ИнформационныеБазыСерверы.АгентЦКК = &АгентЦКК
	               |	
	               |	ОБЪЕДИНИТЬ ВСЕ
	               |	
	               |	ВЫБРАТЬ
	               |		АгентЦКК.ТестСбораСчетчиков,
	               |		АгентЦКК.ТестСбораСчетчиков
	               |	ИЗ
	               |		Справочник.АгентыЦКК КАК АгентЦКК
	               |	ГДЕ
	               |		АгентЦКК.Ссылка = &АгентЦКК) КАК СостояниеИБ";
	
	Запрос.УстановитьПараметр("АгентЦКК", АгентЦКК);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Если Выборка.Следующий() Тогда
		Если Выборка.ВключенСборСчетчиков Тогда
			Если Не Выборка.ТестСбораСчетчиков Тогда
				СчетчикиАгента = Справочники.ИнформационныеБазы.ПолучитьСчетчикиАгента(АгентЦКК);
				Результат = (СчетчикиАгента.Количество() > 0);
			Иначе
				АгентЦККОбъект = АгентЦКК.ПолучитьОбъект();
				АгентЦККОбъект.ТестСбораСчетчиков = Ложь;
				АгентЦККОбъект.Записать();
				Результат = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // ВключенСборСчетчиков()

// Снимает или устанавливает признак необходимости сбора счетчиков
// производительности Агентом ЦКК
//
// Параметры:
//  ИБ - СправочникСсылка.ИнформационныеБазы - Информационная база
//  ВключитьСбор  - Булево - Значение признака
//
Процедура ВключитьСборСчетчиков(ИБ, ВключитьСбор) Экспорт
	
	НаборЗаписей = РегистрыСведений.СостояниеСбораПоказателей.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ИнформационнаяБаза.Установить(ИБ);
	
	НоваяСтрока = НаборЗаписей.Добавить();
	НоваяСтрока.ИнформационнаяБаза = ИБ;
	НоваяСтрока.ВключенСборСчетчиков = ВключитьСбор;
	
	НаборЗаписей.Записать(Истина);
	
КонецПроцедуры // ВключитьСборСчетчиков()
